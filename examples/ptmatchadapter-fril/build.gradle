buildscript {
  ext {
    springBootVersion = '1.4.0.RELEASE'
  }
  repositories {
    mavenCentral()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    // Note: version 0.12 appears to be broken. 
    classpath('com.moowork.gradle:gradle-gulp-plugin:0.11')
  }
}

// $gradle gulp_build    # this runs gulp build
// see https://github.com/srs/gradle-node-plugin
apply plugin: 'com.moowork.gulp'


apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'spring-boot' 


jar {
  baseName = 'ptmatchadapter-fril'
  version = '0.0.1-SNAPSHOT'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

//configure your version of node.
node {
    // Version of node to use.
    version = '5.11.0'
    download = true
}



dependencies {
  compile project(':ptmatchadapter-common')
  //compile('org.springframework.boot:spring-boot-starter')
  compile('org.springframework.boot:spring-boot-starter-web')

  // Forces Spring version to be the version specified in this project
  runtime group: 'org.springframework', name: 'spring-beans', version: "${springVersion}"
  runtime group: 'org.springframework', name: 'spring-core', version: "${springVersion}"
  runtime group: 'org.springframework', name: 'spring-context', version: "${springVersion}"
  runtime group: 'org.springframework', name: 'spring-aop', version: "${springVersion}"
  runtime group: 'org.springframework', name: 'spring-expression', version: "${springVersion}"
  runtime group: 'org.springframework', name: 'spring-web', version: "${springVersion}"
  runtime group: 'org.springframework', name: 'spring-webmvc', version: "${springVersion}"

  compile "org.apache.camel:camel-spring-boot:${camelVersion}"
  compile "org.apache.camel:camel-http4:${camelVersion}"
  compile "org.apache.camel:camel-jetty:${camelVersion}"
  compile "org.apache.camel:camel-stream:${camelVersion}"
  compile "org.apache.camel:camel-jaxb:${camelVersion}"
  compile "org.apache.camel:camel-jackson:${camelVersion}"
  // Camel-swagger-java has bugs affecting use w/ Spring Boot in 2.16.2
  // Try again in next Camel update
  compile "org.apache.camel:camel-swagger-java:${camelVersion}"

  compile 'org.apache.commons:commons-csv:1.2+'
  compile 'org.apache.commons:commons-collections4:4.1+'

  compile "ca.uhn.hapi.fhir:hapi-fhir-base:${hapiFhirVersion}"
  compile "ca.uhn.hapi.fhir:hapi-fhir-structures-hl7org-dstu2:${hapiFhirVersion}"

  compile 'com.samskivert:jmustache:1.12+'
  
  // Required by MITRE ID COnnect; fails if we don't grab it explicitly
  compile group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.52+'
  // MITRE ID Connect lib provides classes that work with JWK
  compile group: 'org.mitre', name: 'openid-connect-common', version: "${mitreidConnectVersion}"
  
  compile 'org.jolokia:jolokia-spring:1.3.3'
  compile ('org.springframework.boot:spring-boot-actuator')
  
  runtime ('de.huxhorn.lilith:de.huxhorn.lilith.logback.appender.multiplex-classic:8.0.0')

  compile "org.slf4j:slf4j-api:${slf4jVersion}"

  testCompile "junit:junit:${junitVersion}"
  testCompile('org.springframework.boot:spring-boot-starter-test')
  
  // Dependencies needed for FRIL
  compile files('lib/fril-2.1.5.1.jar')
  runtime files('lib/emory-util-all.jar')
  runtime files('lib/janino.jar')
  runtime files('lib/libsvm.jar')
  runtime files('lib/rsyntaxtextarea.jar')

  runtime 'commons-logging:commons-logging:1.2'
  runtime 'log4j:log4j:1.2.17+'

  runtime 'au.com.bytecode:opencsv:2.4'

  runtime 'org.apache.poi:poi:3.13'
  runtime 'org.apache.poi:poi-scratchpad:3.13'
  runtime 'org.apache.poi:poi-contrib:3.6'

  runtime 'org.swinglabs:swing-layout:1.0.3'
  runtime 'org.swinglabs:swingx:1.6.1'
  
  //See https://github.com/TeamCohen/secondstring
  runtime 'com.wcohen:com.wcohen.secondstring:0.1'

  // Something used by FRIL depends on xml-apis v1.4.x
  runtime 'xml-apis:xml-apis:1.4.01'
  runtime 'xerces:xercesImpl:2.11.+'
}


//make sure node and gulp are installed before calling the build task in gulpfile.js
gulp_build.dependsOn 'installGulp'
gulp_build.dependsOn 'npmInstall'

//make sure gulp generates artifacts for the build
// before resources are processed
processResources.dependsOn gulp_build


// mainClassName is used by application plugin
mainClassName = 'org.mitre.ptmatchadapter.PtmatchAdapter'


springBoot {
    mainClass = 'org.mitre.ptmatchadapter.PtmatchAdapter'
}
